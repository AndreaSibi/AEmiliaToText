ARCHI_TYPE FTASystem_Type(
		   const rate msg_gen_rate := 5,	
		   const rate forward_rate := 5,	
		   const rate prop_rate := 5,	
		   const weight stop_prob := 0.2)	
			

ARCHI_ELEM_TYPES

    ELEM_TYPE FTA_Type(const rate forward_rate)
	
		BEHAVIOR
 

			Forward( void; void) = 
					<inFTA1, _> . <forward, exp(forward_rate)> . <outFTA1, inf> . Forward()
	
		INPUT_INTERACTIONS	
					
			UNI inFTA1

		OUTPUT_INTERACTIONS
					
			UNI outFTA1

    ELEM_TYPE NetHi_Type(const rate prop_rate)
	
		BEHAVIOR
 

			Transmit( void; void) = 
					choice
					{
						<inNetH1, _> . <downstream, exp(prop_rate)> . <outNetH1, inf> . Transmit(),
						<inNetH2, _> . <upstream, exp(prop_rate)> . <outNetH2, inf> . Transmit()		
					}
	
		INPUT_INTERACTIONS	
					
			UNI inNetH1;
			inNetH2

		OUTPUT_INTERACTIONS
					
			UNI outNetH1;
			outNetH2

    ELEM_TYPE MobApp_Type(void)
	
		BEHAVIOR
 

			GetVideo( void; void) = 
					<inMA1, _> . <getVideo, inf> . GetVideo()
	
		INPUT_INTERACTIONS	
					
			UNI inMA1

		OUTPUT_INTERACTIONS
					
			UNI getVideo

    ELEM_TYPE Robot_Type(const rate msg_gen_rate,const weight stop_prob)
	
		BEHAVIOR
 

			SendVideo( void; void) = 
					<sendVideo, exp(msg_gen_rate)> . <outR1, inf (1, stop_prob)> . SendVideo()
	
		INPUT_INTERACTIONS	
					
			UNI sendVideo

		OUTPUT_INTERACTIONS
					
			UNI outR1

				

ARCHI_TOPOLOGY
				
           ARCHI_ELEM_INSTANCES
					FTA : FTA_Type( forward_rate);
                    NETH : NetHi_Type( prop_rate);
                    MA : MobApp_Type();
                    R : Robot_Type( msg_gen_rate, stop_prob)

		   ARCHI_INTERACTIONS	
					MA.getVideo;
					R.sendVideo

		   ARCHI_ATTACHMENTS
					FROM R.outR1 TO NETH.inNetH1;
					FROM NETH.outNetH1 TO FTA.inFTA1;
					FROM FTA.outFTA1 TO NETH.inNetH2;
					FROM NETH.outNetH2 TO MA.inMA1
				
END

